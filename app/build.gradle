apply plugin: "com.android.application"

ext.trc_version = "0.4.0"

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("commons-io:commons-io") with module("com.github.WycliffeAssociates:tR_Converter:$trc_version")
        substitute module("org.json:json") with module("com.github.WycliffeAssociates:tR_Converter:$trc_version")
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "bible.translationtools.converter"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 19
        versionName "1.6.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        if(project.hasProperty('keystorePath')
                && project.hasProperty('storePass')
                && project.hasProperty('keyPass')) {
            release {
                storeFile file(keystorePath)
                storePassword storePass
                keyAlias "translationRecorderAndroidKey"
                keyPassword keyPass
            }
        } else {
            release {}
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildToolsVersion "28.0.0"
}

dependencies {
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.android.support:support-v4:28.0.0"
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.github.WycliffeAssociates:tR_Converter:$trc_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.1"
    androidTestImplementation "com.android.support.test:runner:1.0.1"
    testImplementation "junit:junit:4.12"
}
